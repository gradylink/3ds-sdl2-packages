# Maintainer: Grady Link <homebrew@grady.link>

pkgname=3ds-sdl2_mixer
pkgver=2.8.1
pkgrel=1
pkgdesc="A sample multi-channel audio mixer library."
arch=(any)
url="https://libsdl.org/projects/SDL_mixer/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('3ds-cmake' 'dkp-toolchain-vars')
depends=('wut' '3ds-sdl2')
source=("${url}/release/SDL2_mixer-${pkgver}.tar.gz")
groups=('3ds-portlibs' '3ds-sdl2-libs')

build() {
  cd "SDL2_mixer-${pkgver}"

  mkdir build
  cd build

  source /opt/devkitpro/3dsvars.sh

  arm-none-eabi-cmake .. \
    -DCMAKE_BUILD_TYPE="Release" \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DSDL2MIXER_VORBIS=STB \
    -DSDL2MIXER_MP3=MINIMP3 \
    -DSDL2MIXER_FLAC=DRFLAC \
    -DSDL2MIXER_MOD=OFF \
    -DSDL2MIXER_OPUS=OFF \
    -DSDL2MIXER_FLUIDSYNTH=OFF \
    -DSDL2MIXER_WAVPACK=OFF \
    -DSDL2MIXER_MIDI=OFF

  make
}

package() {
  cd "SDL2_mixer-${pkgver}/build"
  make install DESTDIR=$pkgdir
}

sha256sums=('cb760211b056bfe44f4a1e180cc7cb201137e4d1572f2002cc1be728efd22660')
